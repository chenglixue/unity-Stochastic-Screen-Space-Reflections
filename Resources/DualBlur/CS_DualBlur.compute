#pragma region Kernel
#pragma kernel DualBlurDownSample
#pragma kernel DualBlurUpSample
#pragma kernel LerpDownUpTex
#pragma endregion

Texture2D<float4> _SourceTex;
RWTexture2D<float4> _RW_TargetTex;
float4 _SourceSize;
float4 _TargetSize;
SamplerState sampler_LinearClamp;
float _BlurOffset;

// from https://www.shadertoy.com/view/ct3Szl

float4 SampleSourceTex(float2 uv, float2 offset)
{
    return _SourceTex.SampleLevel(sampler_LinearClamp, uv + offset * _BlurOffset, 0);
}

[numthreads(8,8,1)]
void DualBlurDownSample (uint3 id : SV_DispatchThreadID)
{
    float2 uv = (float2(id.xy) + 0.5f) * _TargetSize.zw;
    float2 halfPixel = 0.5f * _TargetSize.zw;

    float3 c = SampleSourceTex(uv, float2(0.0f, 0.0f));
    float3 tl = SampleSourceTex(uv, halfPixel * float2(-1.0f, +1.0f));
    float3 tr = SampleSourceTex(uv, halfPixel * float2(+1.0f, +1.0f));
    float3 bl = SampleSourceTex(uv, halfPixel * float2(-1.0f, -1.0f));
    float3 br = SampleSourceTex(uv, halfPixel * float2(+1.0f, -1.0f));

    float3 color = (tl + tr + bl + br + c * 4.0f) / 8.0f;
    
    _RW_TargetTex[id.xy] = float4(color, 1.0f);
}

[numthreads(8,8,1)]
void DualBlurUpSample (uint3 id : SV_DispatchThreadID)
{
    float2 uv = float2(id.xy) * _TargetSize.zw;
    float2 onePixel = 1.0f * _TargetSize.zw;
    
    float3 t2 = SampleSourceTex(uv, onePixel * float2(+0.0f, +2.0f));
    float3 b2 = SampleSourceTex(uv, onePixel * float2(+0.0f, -2.0f));
    float3 l2 = SampleSourceTex(uv, onePixel * float2(-2.0f, +0.0f));
    float3 r2 = SampleSourceTex(uv, onePixel * float2(+2.0f, +0.0f));
    float3 tl = SampleSourceTex(uv, onePixel * float2(-1.0f, +1.0f));
    float3 tr = SampleSourceTex(uv, onePixel * float2(+1.0f, +1.0f));
    float3 bl = SampleSourceTex(uv, onePixel * float2(-1.0f, -1.0f));
    float3 br = SampleSourceTex(uv, onePixel * float2(+1.0f, -1.0f));

    float3 color = (t2 + b2 + l2 + r2 + 2.0f * (tl + tr + bl + br)) / 12.0f;
    _RW_TargetTex[id.xy] = float4(color, 1.0f);
}

[numthreads(8,8,1)]
void LerpDownUpTex (uint3 id : SV_DispatchThreadID)
{
    float3 sourceColor = _SourceTex.Load(uint3(id.xy, 0)).rgb;
    float3 targetColor = _RW_TargetTex.Load(uint3(id.xy, 0)).rgb;
    float3 resultColor = lerp(sourceColor, targetColor, _BlurOffset).rgb;

    _RW_TargetTex[id.xy] = float4(resultColor, 1.f);
}
